
task clean(type: Delete) {
    group = 'build'
    delete("$project.rootDir/pipe-cli/dist")
}

task build(type: Exec, dependsOn: ':pipe-cli:test') {
    group = 'build'
    doFirst {
        setCLIVersion.execute()
    }
    commandLine 'python', "$project.rootDir/pipe-cli/setup.py", 'sdist'
    doLast {
        cleanCLIVersion.execute()
        delete("$project.rootDir/api/src/main/resources/static/PipelineCLI.tar.gz")
        copy {
            from("$project.rootDir/pipe-cli/dist/PipelineCLI-${version}.tar.gz")
            into("$project.rootDir/api/src/main/resources/static/")
            rename { String fileName ->
                fileName.replace("PipelineCLI-${version}", "PipelineCLI")
            }
        }
    }
}

task test(type: Exec, dependsOn: ':pipe-cli:installPackages') {
    group = 'test'
    commandLine "pytest", "-s", "-vvv", "--cov=$project.rootDir/pipe-cli", "--cov-config=$project.rootDir/pipe-cli/tests/cov_config"
}

task installPackages(type: Exec) {
    group = 'other'
    commandLine "pip", "install", "click", "PTable", "requests", "pytz", "tzlocal", "mock", "requests_mock", "pytest", "pytest-cov",  "PyJWT==1.6.1"
}

task setCLIVersion() {
    doLast {
        copy {
            from('src/version.py')
            into('temp')
        }
        def proj = file('src/version.py')
        def text = proj.getText("UTF-8")
        text = text.replaceAll(/(__version__='.+')/, "__version__=\'$version\'")
        proj.write(text, "UTF-8")
    }
}

task cleanCLIVersion() {
    doLast {
        copy {
            from('temp/version.py')
            into('src')
        }
        delete("$project.rootDir/pipe-cli/temp")
    }
}

